plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.github.wnebyte.workoutapp"
        minSdkVersion 24 // 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file('C:/Users/Ralle/dev/keystore/android.jks')
            storePassword System.getenv('KEY_STORE_PW')
            keyAlias 'key1'
            keyPassword System.getenv('KEY_1_PW')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
    }
    flavorDimensions 'version'
    productFlavors {
        free {
            dimension 'version'
            applicationIdSuffix '.free'
            versionNameSuffix '-free'
        }
        pro {
            dimension 'version'
            applicationIdSuffix '.pro'
            versionNameSuffix '-pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "schema")
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // room
    def room_version = "2.4.0-alpha02"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // androidx
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.preference:preference:1.1.1'

    // android
    implementation 'android.arch.work:work-runtime:1.0.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'

    // third party
    implementation 'jp.wasabeef:recyclerview-animators:4.0.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // free
    freeImplementation 'com.google.android.gms:play-services-ads:20.4.0'
}